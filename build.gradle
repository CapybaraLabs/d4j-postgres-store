plugins {
	id "org.jetbrains.kotlin.jvm" version "1.5.31"
	id "java-library"
	id "com.github.ben-manes.versions" version "0.39.0"
	id "maven-publish"
	id "jacoco"
	id "org.sonarqube" version "3.3"
	id "com.adarshr.test-logger" version "3.0.0"
}

group = "dev.capybaralabs"
version = "0.0.1-SNAPSHOT"

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions.jvmTarget = 11
}

repositories {
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // D4J snapshots
	maven { url "https://repo.spring.io/milestone" }                        // D4J snapshots
}

dependencies {
	implementation platform("org.jetbrains.kotlin:kotlin-bom")
	implementation platform("org.springframework.boot:spring-boot-dependencies:2.5.5")
	testImplementation platform("org.testcontainers:testcontainers-bom:1.16.0")

	api "com.discord4j:discord4j-core:3.2.0"
	api "io.r2dbc:r2dbc-postgresql:0.8.10.RELEASE"
	api "io.r2dbc:r2dbc-pool:0.8.7.RELEASE"
	api "org.slf4j:slf4j-api"

	implementation "org.jetbrains.kotlin:kotlin-reflect"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation "io.projectreactor.kotlin:reactor-kotlin-extensions"

	testImplementation "org.jetbrains.kotlin:kotlin-test"
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testImplementation "org.assertj:assertj-core"
	testImplementation "org.testcontainers:postgresql"
	testImplementation "org.testcontainers:r2dbc"
	testImplementation "ch.qos.logback:logback-classic"
	testImplementation "io.projectreactor:reactor-tools"
}

test {
	useJUnitPlatform()
	systemProperty "junit.jupiter.execution.parallel.enabled", "true"
	systemProperty "junit.jupiter.execution.parallel.mode.default", "concurrent"
	systemProperty "junit.jupiter.execution.parallel.mode.classes.default", "concurrent"
}

testlogger {
	theme "mocha-parallel"
	slowThreshold 500
	showFullStackTraces true
}

jacocoTestReport {
	reports {
		xml.required.set(true)
	}
}

tasks.withType(org.sonarqube.gradle.SonarQubeTask) {
	dependsOn jacocoTestReport
}

def isNonStable = { String version ->
	def stableKeyword = ["RELEASE", "FINAL", "GA"].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
	checkConstraints = true
	rejectVersionIf {
		isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
	}
}

publishing {
	publications {
		create("main", MavenPublication) {
			from(components.kotlin)
		}
	}
}
