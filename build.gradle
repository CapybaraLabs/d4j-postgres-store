plugins {
	id "org.jetbrains.kotlin.jvm" version "1.5.31"
	id "com.github.ben-manes.versions" version "0.39.0"
	id "org.sonarqube" version "3.3"
	id "com.adarshr.test-logger" version "3.0.0" apply false
}


allprojects {
	apply plugin: "jacoco"

	group = "dev.capybaralabs.d4j-store"
	version = "0.1.2-SNAPSHOT"

	repositories {
		mavenCentral()
//		maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // D4J snapshots
//		maven { url "https://repo.spring.io/milestone" }                        // D4J snapshots
	}
}

subprojects {
	apply plugin: "maven-publish"
}

configure(subprojects.findAll { it.name != 'platform' }) {
	apply plugin: "org.jetbrains.kotlin.jvm"
	apply plugin: "com.adarshr.test-logger"

	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
		kotlinOptions.jvmTarget = 11
	}
	dependencies {
		api platform(project(':platform'))
		annotationProcessor platform(project(':platform'))
		compileOnly platform(project(':platform'))
	}

	testlogger {
		theme "mocha-parallel"
		slowThreshold 500
		showFullStackTraces true
	}

	publishing {
		publications {
			create("main", MavenPublication) {
				from(components.kotlin)
			}
		}
	}
}

project(':platform') {
	apply plugin: 'java-platform'
	javaPlatform {
		allowDependencies()
	}
	dependencies {
		api platform("org.jetbrains.kotlin:kotlin-bom:1.5.31")
		api platform("org.springframework.boot:spring-boot-dependencies:2.5.5")
		api platform("org.testcontainers:testcontainers-bom:1.16.0")
		api platform("org.junit:junit-bom:5.8.1")
		constraints {
			api "com.discord4j:discord4j-core:3.2.0"
			api "io.r2dbc:r2dbc-postgresql:0.8.10.RELEASE"
			api "io.r2dbc:r2dbc-pool:0.8.7.RELEASE"
		}
	}

	publishing {
		publications {
			create("main", MavenPublication) {
				from(components.javaPlatform)
			}
		}
	}
}

// See https://github.com/gradle/gradle/issues/8881#issuecomment-593227192
task codeCoverageReport(type: JacocoReport) {
	// Gather execution data from all subprojects
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
	// Add all relevant sourcesets from the subprojects
	subprojects.each {
		sourceSets it.sourceSets.main
	}
	reports {
		xml.required.set(true)
		html.required.set(true)
		csv.required.set(false)
	}
}
// always run the tests before generating the report
codeCoverageReport.dependsOn {
	subprojects.findAll { it.name != 'platform' }*.test
}
tasks.withType(org.sonarqube.gradle.SonarQubeTask) {
	dependsOn codeCoverageReport
}
sonarqube {
	properties {
		property "sonar.projectKey", "dev.capybaralabs.d4j.store.postgres:d4j-postgres-store"
		property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
	}
}

def isNonStable = { String version ->
	def stableKeyword = ["RELEASE", "FINAL", "GA"].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
	checkConstraints = true
	rejectVersionIf {
		isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
	}
}
